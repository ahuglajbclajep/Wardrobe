apply plugin: 'com.android.application'
apply plugin: 'checkstyle'
apply plugin: 'findbugs'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.example.wardrobe"
        minSdkVersion 19
        targetSdkVersion 19
        versionCode 1
        versionName "1.0.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
    }
}

checkstyle.toolVersion = '8.5'
findbugs.toolVersion = '3.0.1'

task checkstyle(type: Checkstyle) {
    ignoreFailures = true
    configFile = file "${rootDir}/config/checkstyle/checkstyle.xml"

    source = fileTree 'src/main'
    classpath = files()
    reports {
        html.enabled = true
        xml.enabled = !html.enabled
    }
}

task findbugs(type: FindBugs, dependsOn: assemble) {
    ignoreFailures = true
    excludeFilter = file "${rootDir}/config/findbugs/exclude_filter.xml"
    jvmArgs = ['-Duser.language=en']

    source = fileTree 'src/main'
    classpath = files()
    classes = files "${buildDir}/intermediates/classes/"
    reports {
        html.enabled = true
        xml.enabled = !html.enabled
    }
}

check.dependsOn 'findbugs', 'checkstyle'

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:27.0.2'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:design:27.0.2'

    implementation 'com.journeyapps:zxing-android-embedded:3.5.0'
    implementation 'com.github.bumptech.glide:glide:4.3.1'
    implementation 'com.github.chuross:aspect-ratio-imageview:v1.0.2'
}
